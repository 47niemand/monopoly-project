plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
}

group 'pp.muza.monopoly'
version '0.1'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    // Use Lombok for generating getters and setters.
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'ch.qos.logback:logback-classic:1.2.6'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    implementation 'commons-cli:commons-cli:1.5.0'
}

application {
    // Define the main class for the application.
    mainClass = 'pp.muza.monopoly.App'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

tasks.register('simJar', Jar) {
    archiveClassifier = 'sim'

    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    manifest {
        attributes("Implementation-Title": "Monopoy Game Simulator",
                "Implementation-Version": archiveVersion,
                "Main-Class": application.mainClass

        )
    }
}
